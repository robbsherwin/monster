!\---------------------------------------------------------------------------

        Monster by a Whisper
        Robb Sherwin
				11.15.2019

---------------------------------------------------------------------------\!

! Uncomment the following to use a precompiled version of the Hugo Library:
! #set PRECOMPILED_LIBRARY

! Uncomment the following to include the HugoFix Debugging Library:
! #set DEBUG

! Uncomment the following to include verb stub routines:
! #set VERBSTUBS

#ifset DEBUG
#switches -d
#endif

!// Verbs
verb "catch", "grab"
  * object DoCatch
  * DoCatch

verb "jump", "hop"
  * DoJump

verb "pick"
  * DoVague
  * object DoVague
  * object "with" xobject DoPick

  verb "scan"
    * DoScan
    * "room"/"location"/"area" DoScan

!// Verbs

#include "roodylib.g"
#include "verblib.g"			! grammar must come first
#include "hugolib.h"
#include "roodylib.h"

#include "glk.h"
#include "introscreen.hug"
#include "system.h"
#include "resource.h"
#include "robblibrary.h"
#include "daemons.hug"
#include "help.hug"
#include "objects.hug"
#include "routines.hug"
#include "variables.hug"
#include "window.h"
#include "locations.hug"

global GRAPHICS_ON = TRUE
global last_drawn_picture
global force_repaint
global current_resource_file
global current_resource
global old_screenwidth
global temp_foregroundcolor
global temp_backgroundcolor

global DEFAULT_TEXTCOLOR !used for the help file
global DEFAULT_BGCOLOR !used for the help file

property graphic

global testValue = 0 !// Just used when we need a spare use-once variable

constant MAXSONGS 1
array temparray[10] = 0,0,0,0,0,0,0,0,0,0
array MusicListingArray[MAXSONGS]
array randomArgs[7]

constant MAX_GAME_EVENTS 50
array GameEvents[MAX_GAME_EVENTS]
! GameEvents[0] = Has Ja'Rod worn the x-ray specs yet?
! GameEvents[1] = Have we shown the Alexandra graphic yet?
! GameEvents[2] = Did Ja'Rod talk to Carl for the first move?
! GameEvents[3] = Are we OK to use the catch verb?
! GameEvents[4] = Did Ja'Rod pick his lock?
! GameEvents[5] = Did Ja'Rod pick the lock that is keeping Alexandra bound?
! GameEvents[6] = Does Ja'Rod know the PIN for the safe underneath the sink in TheExecutionRoom ?

routine init
{
  InitializeGameEvents()

  FORMAT = FORMAT | DESCFORM_F | NOINDENT_F

  display.title_caption = "Monster by a Whisper by Robb Sherwin"

	counter = -1

  glkcheck()

	STATUSTYPE = 0

	!TEXTCOLOR = DEF_FOREGROUND
	!BGCOLOR = DEF_BACKGROUND
	!SL_TEXTCOLOR = DEF_SL_FOREGROUND
  !SL_BGCOLOR = DEF_SL_BACKGROUND

  TEXTCOLOR = 15
  BGCOLOR = 13
  color TEXTCOLOR, BGCOLOR

	prompt = ">"

  NewMenu()

  cls

  DEFAULT_FONT	= PROP_ON
  Font(DEFAULT_FONT)

  player = JaRod                    ! player initialization
  location = TheExecutionRoom
  old_location = location

  move player to location
  FindLight(location)

  !// He's chained up to start the game
  JaRod is incapacitated

  locate 1,10

  DescribePlace(location)
  location is visited
  CalculateHolding(player)

  old_screenwidth = display.screenwidth

  #ifset USE_PLURAL_OBJECTS
  InitPluralObjects
  #endif
}

#ifset PRECOMPILED_LIBRARY
replace main
{
#else
routine main
{
#endif
	counter = counter + 1
	PrintStatusLine
	run location.each_turn
	runevents
	RunScripts
	if parent(speaking)~=location
		speaking = 0
}

player_character JaRod "I"
{
pronoun "I" "me" "my"
nouns "jarod" "me" "self" "ja'rod" "butler"
adjectives "ja'rod" "jarod"

	long_desc
	{
		"I am a twenty-four year old Ethiopian male, I am 5'9\" tall and I weigh 145 pounds. I have black hair and brown eyes. I am wearing a white T-shirt, blue jeans and grey moon boots for shoes. My
    hair is buzzed pretty tightly against my head and I have a little bit of beard stubble."
	}
}

character NPC
{
type NPC

is living

maxhitpoints 10
hitpoints 10
armorclass 10
attackadjust 0
damagepotential 1
peaceful true

	before
	{
		object DoLook
		{
		!UpdateFaceWindow(self)

		run self.long_desc
		if (self.conscious = 0) and (self is not female) and (self.hitpoints <= 0) and (self.hitpoints > -100)
		{
			"\n";
			CPronoun(self)
			"is currently ";

			! Would have to do this again below if any female characters can be drunk
			if self is drunk
			{
				"passed ";
			}
			else
			{
				"knocked ";
			}

			"out."
		}

		if (self.conscious = 0) and (self is not female) and (self.hitpoints <= -100)
		{
			"\n";
			CPronoun(self)
			"is dead."
		}

		if (self.conscious = 0) and (self is female) and (self.hitpoints <= 0) and (self.hitpoints > -100)
			"\nShe is currently knocked out."

		if (self.conscious = 0) and (self is female) and (self.hitpoints <= -100)
			"\nShe is dead."

		}
		object DoWakeCharacter
		{
			if self.conscious = 1
			{
				"That's not necessary."
			}
			else
			{
				"You try, but you don't get any sort of response back into consciousness."
			}
		}

		object DoKiss
		{
			"That's not appropriate."
		}
		object DoListen
		{
			"Nothing out of the ordinary."
		}

	}

in_scope
	{ if self in location : return 1
          else : return 0
        }

conscious
	{ if self.hitpoints > 0 : return 1
	  else : return 0
	}

}

resource "MONST01"
{
	"jpg/titlescreen01.jpg"
  "jpg/jarod01.jpg"
  "jpg/alexandra01-2560x1440.jpg"
}

#include "characters.hug"
