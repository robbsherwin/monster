object xraySpecs "x-ray specs"
{
nouns "specs" "x-ray"
adjectives "x-ray"
article "the" "my"
!// Not special is up, special is down
is clothing, hidden, not special, plural
in TheExecutionRoom

	long_desc
	{
		"The X-ray specs are glasses that allow me to perform a scan of where I am. They work as a miniaturized X-ray machine at that point:
		the X-ray radiation gives me a view of the world that allows me to see through some walls and some flesh, and the electronics inside the
		specs give me an understanding of the chemical composition of things that I am looking at.\n"

		"The X-ray specs are currently " ;
		if self is special
		{
			"lowered."
		}
		else {
			"raised."
		}
	}
	before
	{
		object DoGet
		{
			move self to player
			if GameEvents[7] = 0
			{
				GameEvents[8] = 1
				"I take my X-ray specs. I'm glad to have them back! I guess they just thought they were junk when we got captured.
			   I just need to wear them and then a sense of the normal around here will return somewhat."
			}
			else {
				"Taken."
			}
		}
		object DoDrop
		{
			"They are essential for me to do my job, I shouldn't be without them."
		}
		object DoLower
		{
			if self is special
			{
				"They are already lowered!"
			}
			else {
				self is special
				"Okay, I've lowered my X-ray specs."
			}
		}
		object DoRaise
		{
			if self is not special
			{
				"They are already raised."
			}
			else {
				self is not special
				"My X-ray specs are now raised."
			}
		}
    object DoWear
    {
      if GameEvents[0] = 0
      {
        GameEvents[0] = 1
        "I put them on. I feel complete, again."
				self is worn

	       if (display.hasgraphics) and GRAPHICS_ON = TRUE {
            ""
            "[Strike any key to continue.]"
            pause

            temp_foregroundcolor = TEXTCOLOR
            temp_backgroundcolor = BGCOLOR
            TEXTCOLOR = 0
            BGCOLOR = 0
            color TEXTCOLOR, BGCOLOR

            cls
            picture "MONST01", "jarod01"
            pause

            TEXTCOLOR = temp_foregroundcolor
            BGCOLOR = temp_backgroundcolor
            color TEXTCOLOR, BGCOLOR

            cls
            Perform(&DoLook, location)
          }

      }
      else {
        "Worn."
      }
    }
	}
}

object bobbyPin "bobby pin"
{
nouns "pin" "lockpick" "pick"
adjective "bobby"
article "the"

	long_desc
	{
		"It's actually a pretty nice, machine-tumbled clip that allows anyone to hold their hair in a certain place. Maybe about four
		inches long? I don't have enough hair on my head* to need it for that but it could be helpful with other things.\n"
		"* Or elsewhere!"
	}

	!// If we drop it ... we shouldn't be able to just pick it up.
}

object padlock "padlock"
{
nouns "padlock" "lock"
article "the"
key_object bobbyPin
is lockable, locked, hidden
in TheExecutionRoom

	long_desc
	{
		"Not a great looking lock, I don't think - probably pickable if I had the right tool"
	}
	before {
		object DoPick
		{
			if bobbyPin in player
			{
				if GameEvents[4] = 0
				{
					self is not locked
					player is not incapacitated
					remove padlock
					move alexPadlock to location

					"\I(So you know, I've done three magic tricks in my life. And only three. This is the first one, escaping some handcuffs.)\i\n"

					"It takes about 15 seconds of fidgeting with the bobby pin and I am able to completely pick the padlock. It falls to the floor. I'm able to catch
					the bobby pin as the lock drops.\n"

					"\"I did it!\" I say to Alexandra, looking for some confirmation at how wicked I am. She just nods quietly. She is really not doing well.\n"

					"I'm able to take a look around the room - there is a sink and counter that was behind us. My X-ray specs are sitting by themselves on the counter.
					They must not have known what they are and how useful they are."

					!// She can't talk right now
					Alexandra.talkValue = 2
				}
				else {
					"I have already done that with my padlock."
				}
			}
			else {
				"I don't have the right tool!"
			}
		}
		object DoLock
		{
			"I have no need to re-lock the padlock."
		}
	}
}

object alexPadlock "padlock"
{
	nouns "padlock" "lock"
	article "alexandra's", "the"
	key_object bobbyPin
	is lockable, locked, hidden

	long_desc
	{
		"The lock keeping Alexandra bound looks just as low-quality as my lock did."
	}
	before
	{
			object DoPick
			{
				if bobbyPin in player
				{
					if GameEvents[5] = 0
					{
						GameEvents[5] = 1
						"It takes me about ten seconds to align the tumblers in the padlock keeping Alexandra chained up. (I'm a big lockpick guy now, totally understand what's going on with these things, as can be seen.)\n"
						"Alexandra's hands fall limply to her side. I manage to help her down. I think she used up all her energy when the Sunset's Commander was here, I definitely need to see if I can get her some help."
					}
					else {
						"I don't need to do that again."
					}
				}
				else {
					"I don't have anything that can help pick Alexandra's padlock."
				}

			}
			object DoLock
			{
					"That doesn't help me or my situation."
			}
	}
}
